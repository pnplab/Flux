// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply from: 'aware-core/aware.gradle'

project.ext {
    aware_libs = (System.getenv("aware_libs") as String ?: "master-SNAPSHOT")
    supportLibVersion = "28.0.0"
}

buildscript {
    ext {
        buildToolsVersion = "28.0.3"
        minSdkVersion = 19 // @note 16 -> 19 because of Aware minSdkVersion requirements.
        compileSdkVersion = 28
        targetSdkVersion = 27
        supportLibVersion = "28.0.0"
        kotlin_version = "1.3.30" // For aware-core & Detox
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'com.google.gms:google-services:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

}

// Fix `Execution failed for task ':react-native-appupdate:verifyReleaseResources'`
subprojects {
    afterEvaluate {project ->
        if (project.hasProperty("android")) {
            android {
                // Fix `Execution failed for task ':react-native-appupdate:verifyReleaseResources'`
                //compileSdkVersion ${buildscript.ext.compileSdkVersion}
                //buildToolsVersion "${buildscript.ext.buildToolsVersion}"
                compileSdkVersion 28
                buildToolsVersion "28.0.3"

                // Fix test compilation for react-native-video using `detox build` cli.
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

            }
        }
    }
}

allprojects {
    repositories {
        // Add jitpack repository (added by react-native-spinkit)
        maven { url "https://jitpack.io" }
        mavenLocal()
        google()
        jcenter()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            // All of Detox' artifacts are provided via the npm module
            url "$rootDir/../node_modules/detox/Detox-android"
        }

        // Fix lottie-react-native build issue.
        // Fix `Execution failed for task ':react-native-appupdate:verifyReleaseResources'`
        configurations.all {
            resolutionStrategy {
                force "com.android.support:appcompat-v7:$supportLibVersion"
                force "com.android.support:support-v7:$supportLibVersion"
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionUrl = distributionUrl.replace("bin", "all")
}
